_ = require('lodash')

export ometa OData2AbstractSQL {

	Process :method =
		:path
		end
		(	?_.isEmpty(path)
			-> []
		|	PathSegment(method, path):query
			(	?(method == 'GET')
				-> ['SelectQuery'].concat(query)
			|	?(method == 'PUT')
				-> ['UpsertQuery'].concat(query)
			|	?(method == 'POST')
				-> ['InsertQuery'].concat(query)
			|	?(method == 'DELETE')
				-> ['DeleteQuery'].concat(query)
			)
		)
	,
	PathSegment :method :path =
		?path.resource
		Resource(path.resource):resource
		{[['From', resource.tableName]]}:query
		(	?path.property
			Resource(path.property.resource):propertyResource
			PathSegment(method, path.property):childQuery
			{query.concat(childQuery)}:query
			-> query.push(['Where', ['Equals', ['ReferencedField', propertyResource.tableName, propertyResource.idField], ['ReferencedField', resource.tableName, propertyResource.tableName]]])
		|	?path.link
			Resource(path.link.resource):linkResource
			{['ReferencedField', resource.tableName, linkResource.tableName]}:referencedField
			(	?path.link.key
				-> query.push(['Where', ['Equals', referencedField, ['Number', path.key]]])
			)?
			-> query.push(['Select', [referencedField]])
		|	?(method == 'PUT' || method == 'POST')
			Fields(resource.tableName, resource.fields):fields
			-> query.push(['Fields', fields])
		|	-> query.push(['Select', [[resource.tableName, '*']]])
		)

		(	?(!path.options)
		|	(	?(!path.options.$filter)
			|	Boolean(path.options.$filter):filter
				-> query.push(['Where', filter])
			)
			(	?(!path.options.$orderby)
			|	OrderBy(path.options.$orderby.properties):orderby
				-> query.push(['OrderBy'].concat(orderby))
			)
		)

		(	?path.key
			-> query.push(['Where', ['Equals', ['ReferencedField', resource.tableName, resource.idField], ['Number', path.key]]])
		)?
		-> query
	,

	OrderBy =
		[	(	:ordering
				-> [ordering.order.toUpperCase(), ['Field', ordering.name]]
			)+:orderby
		]
		-> orderby
	,

	Fields :tableName =
		[	(	:field
				-> [field.fieldName,['Bind', tableName, field.fieldName]]
			)*:fields
		]
		-> fields
	,

	Resource :resourceName =
			{this.clientModel.resources[resourceName]}:resource
			?resource
			(	?resource.tableName
			|	ResourceMapping(resourceName):resourceMapping
				{resource.tableName = resourceMapping._name}
			)
			-> resource
		|	{throw 'Unknown resource: ' + resourceName}
	,

	ResourceMapping :resourceName =
			?this.clientModel.resourceToSQLMappings[resourceName]
			-> this.clientModel.resourceToSQLMappings[resourceName]
		|	{throw 'Unknown resource: ' + resourceName}
	,

	Boolean =
			[	(	'eq'
					-> 'Equals'
				|	'ne'
					-> 'NotEquals'
				|	'gt'
					-> 'GreaterThan'
				|	'ge'
					-> 'GreaterThanOrEqual'
				|	'lt'
					-> 'LessThan'
				|	'le'
					-> 'LessThanOrEqual'
				):operation
				Operand:op1
				Operand:op2
			|	(	'and'
					-> 'And'
				|	'or'
					-> 'Or'
				):operation
				Boolean:op1
				Boolean:op2
			]
			-> [operation, op1, op2]
		|	[	'not'
				Boolean:bool
			]
			-> ['Not', bool]
		|	Property
	,

	Operand =
			Boolean
		|	Number
		|	Text
		|	Math
	,

	Math =
		[	(	'add'
				-> 'Add'
			|	'sub'
				-> 'Subtract'
			|	'mul'
				-> 'Multiply'
			|	'div'
				-> 'Divide'
			):operation
			Operand:op1
			Operand:op2
		]
		-> [operation, op1, op2]
	,

	Property =
		:prop
		?prop.name
		-> ['Field', prop.name]
	,

	Number =
		number:num
		-> ['Number', num]
	,

	Text =
		string:text
		-> ['Text', text]
}