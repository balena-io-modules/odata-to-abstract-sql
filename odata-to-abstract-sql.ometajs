_ = require('lodash')

ometa OData2AbstractSQL {

	Process =
		PathSegment:query
		end
		-> query
	,
	PathSegment =
		:path
		(	?_.isEmpty(path)
			-> []
		|	?path.resource
			{[['From', path.resource]]}:query
			(	?path.property
				PathSegment(path.property):childQuery
				{query.concat(childQuery)}:query
				-> query.push(['Where', ['Equals', ['ReferencedField', path.property.resource, 'id'], ['ReferencedField', path.resource, path.property.resource]]])
			|	?path.link
				{['ReferencedField', path.resource, path.link.resource]}:referencedField
				(	?path.link.key
					-> query.push(['Where', ['Equals', referencedField, ['Number', path.key]]])
				)?
				-> query.push(['Select', referencedField])
			|	-> query.push(['Select', [path.resource, '*']])
			)
			(	?(!path.options)
			|	(	?(!path.options.$filterby)
				|	Boolean(path.options.$filterby):filter
					-> query.push(['Where', filter])
				)
			)
			(	?path.key
				-> query.push(['Where', ['Equals', ['ReferencedField', path.resource, 'id'], ['Number', path.key]]])
			)?
			-> query
		)
	,

	Boolean =
			[	(	'eq'
					-> 'Equals'
				|	'ne'
					-> 'NotEquals'
				|	'gt'
					-> 'GreaterThan'
				|	'ge'
					-> 'GreaterThanOrEqual'
				|	'lt'
					-> 'LessThan'
				|	'le'
					-> 'LessThanOrEqual'
				):operation
				Operand:op1
				Operand:op2
			|	(	'and'
					-> 'And'
				|	'or'
					-> 'Or'
				):operation
				Boolean:op1
				Boolean:op2
			]
			-> [operation, op1, op2]
		|	[	'not'
				Boolean:bool
			]
			-> ['Not', bool]
		|	Property
	,

	Operand =
			Boolean
		|	Number
		|	Text
	,

	Property =
		:prop
		?prop.name
		-> ['Field', prop.name]
	,

	Number =
		number:num
		-> ['Number', num]
	,

	Text =
		string:text
		-> ['Text', text]
}

module.exports = OData2AbstractSQL;
